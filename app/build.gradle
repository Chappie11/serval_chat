apply plugin: 'com.android.application'

def readPassword(String fmt) {
    if (System.console() == null)
        return null;
    return new String(System.console().readPassword(fmt));
}

android {

    compileSdkVersion 25
    buildToolsVersion "25"

    defaultConfig {
        applicationId "org.servalproject.servalchat"
        minSdkVersion 9
        targetSdkVersion 25
        versionCode "git rev-list --count HEAD".execute().text.toInteger();
        versionName "git describe --always --dirty --tags".execute().text.trim();
        resValue "string", "commit_sha", "git rev-parse HEAD".execute().text;
    }

    /* something like this?
    signingConfigs{
        release{
            storeFile = file(RELEASE_STORE_FILE)
            storePassword = readPassword("\n\$ Enter keystore password: ")
            keyAlias = RELEASE_KEY_ALIAS
            keyPassword = readPassword("\n\$ Enter key password: ")
        }
    }

    variantFilter { variant ->
        // TODO test that RELEASE_STORE_FILE is set & valid (or something..)
        if(variant.buildType.name.equals('release') ) {
            variant.setIgnore(true);
        }
    }
    */

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles.add(file('proguard-android.txt'))
            setProperty("archivesBaseName", "ServalChat-$defaultConfig.versionName");
            //signingConfig signingConfigs.release
        }
    }

    // built in ndk support isn't quite ready for what we need, call the old make based ndk-build
    sourceSets.main {
        java.srcDirs += 'src/main/jni/serval-dna/java-api/src'
        jni.srcDirs = [] // prevent autogen of Android.mk
        jniLibs.srcDirs = ['src/main/libs'] // ndk-build output
    }

    task ndkBuild(type: Exec) {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build", '-C', file('src/main/jni').absolutePath, '-j', Runtime.runtime.availableProcessors()
    }

    task ndkClean(type: Exec) {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build", '-C', file('src/main/jni').absolutePath, 'clean'
    }

    clean.dependsOn 'ndkClean'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
}
